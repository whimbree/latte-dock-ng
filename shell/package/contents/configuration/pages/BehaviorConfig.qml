/*
    SPDX-FileCopyrightText: 2016 Smith AR <audoban@openmailbox.org>
    SPDX-FileCopyrightText: 2016 Michail Vourlakos <mvourlakos@gmail.com>
    SPDX-License-Identifier: GPL-2.0-or-later
*/

import QtQuick 2.0
import QtQuick.Layouts 1.3
import org.kde.plasma.core 2.0 as PlasmaCore
import org.kde.plasma.components 3.0 as PlasmaComponents
import org.kde.plasma.components 3.0 as PlasmaComponents3

import org.kde.latte.core 0.2 as LatteCore
import org.kde.latte.components 1.0 as LatteComponents
import org.kde.latte.private.containment 0.1 as LatteContainment

import "../../controls" as LatteExtraControls

PlasmaComponents.Page {
    id: page
    width: content.width + content.Layout.leftMargin * 2
    height: content.height + units.smallSpacing * 2

    ColumnLayout {
        id: content       
        width: (dialog.appliedWidth - units.smallSpacing * 2) - Layout.leftMargin * 2
        spacing: dialog.subGroupSpacing
        anchors.horizontalCenter: parent.horizontalCenter
        Layout.leftMargin: units.smallSpacing * 2

        //! BEGIN: Location
        ColumnLayout {
            Layout.fillWidth: true
            spacing: units.smallSpacing
            Layout.topMargin: units.smallSpacing

            LatteComponents.Header {
                text: screenRow.visible ? i18n("Screen") : i18n("Location")
            }

            Connections {
                target: universalSettings
                function onScreensCountChanged() { screenRow.updateScreens() }
            }

            RowLayout {
                id: screenRow
                Layout.fillWidth: true
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 3
                spacing: 2
                visible: screensCount > 1 || dialog.advancedLevel

                property int screensCount: 1

                function updateScreens() {
                    screensCount = universalSettings.screens.length;
                    screensModel.clear();

                    var primary = {name: i18n("On Primary Screen"), icon: 'favorite'};
                    screensModel.append(primary);

                    var allscreens = {name: i18n("On All Screens"), icon: 'favorite'};
                    screensModel.append(allscreens);

                    var allsecscreens = {name: i18n("On All Secondary Screens"), icon: 'favorite'};
                    screensModel.append(allsecscreens);

                    //check if the screen exists, it is used in cases Latte is moving
                    //the view automatically to primaryScreen in order for the user
                    //to has always a view with tasks shown
                    var screenExists = false
                    for (var i = 0; i < universalSettings.screens.length; i++) {
                        if (universalSettings.screens[i].name === latteView.positioner.currentScreenName) {
                            screenExists = true;
                        }
                    }

                    if (!screenExists && !latteView.onPrimary) {
                        var scr = {name: latteView.positioner.currentScreenName, icon: 'view-fullscreen'};
                        screensModel.append(scr);
                    }

                    for (var i = 0; i < universalSettings.screens.length; i++) {
                        var scr = {name: universalSettings.screens[i].name, icon: 'view-fullscreen'};
                        screensModel.append(scr);
                    }

                    if (latteView.onPrimary && latteView.screensGroup === LatteCore.types.SingleScreenGroup) {
                        screenCmb.currentIndex = 0;
                    } else if (latteView.screensGroup === LatteCore.types.AllScreensGroup) {
                        screenCmb.currentIndex = 1;
                    } else if (latteView.screensGroup === LatteCore.types.AllSecondaryScreensGroup) {
                        screenCmb.currentIndex = 2;
                    } else {
                        screenCmb.currentIndex = screenCmb.findScreen(latteView.positioner.currentScreenName);
                    }

                }

                Connections{
                    target: viewConfig
                    function onShowSignal() { screenRow.updateScreens(); }
                }

                ListModel {
                    id: screensModel
                }

                LatteComponents.ComboBox {
                    id: screenCmb
                    Layout.fillWidth: true
                    model: screensModel
                    textRole: "name"
                    iconRole: "icon"

                    Component.onCompleted: screenRow.updateScreens();

                    onActivated: function(index) {
                        if (index === 0) { // primary
                            latteView.positioner.setNextLocation("", LatteCore.types.SingleScreenGroup, "{primary-screen}", PlasmaCore.Types.Floating, LatteCore.types.NoneAlignment);
                        } else if (index === 1) { // all screens
                            latteView.positioner.setNextLocation("", LatteCore.types.AllScreensGroup, "{primary-screen}", PlasmaCore.Types.Floating, LatteCore.types.NoneAlignment);
                        } else if (index === 2) { // all secondary screens
                            latteView.positioner.setNextLocation("", LatteCore.types.AllSecondaryScreensGroup, "", PlasmaCore.Types.Floating, LatteCore.types.NoneAlignment);
                        } else if (index>2 && (index !== findScreen(latteView.positioner.currentScreenName) || latteView.onPrimary)) {// explicit screen
                            latteView.positioner.setNextLocation("", LatteCore.types.SingleScreenGroup, textAt(index), PlasmaCore.Types.Floating, LatteCore.types.NoneAlignment);
                        }
                    }

                    function findScreen(scrName) {                        
                        for(var i=0; i<screensModel.count; ++i) {
                            if (screensModel.get(i).name === scrName) {
                                return i;
                            }
                        }

                        return 0;
                    }
                }
            }

            RowLayout {
                id: locationLayout
                Layout.fillWidth: true
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2
                Layout.topMargin: screenRow.visible ? units.smallSpacing : 0
                LayoutMirroring.enabled: false
                spacing: 2

                readonly property int buttonSize: (dialog.optionsWidth - (spacing * 3)) / 4

                LatteComponents.Button {
                    id: bottomEdgeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("bottom location", "Bottom")
                    icon.name: "arrow-down"
                    checked: plasmoid.location === edge
                    checkable: true

                    readonly property int edge: PlasmaCore.Types.BottomEdge

                    onClicked: {
                        if (viewConfig.isReady && plasmoid.location !== edge) {
                            latteView.positioner.setNextLocation("", latteView.screensGroup, "", edge, LatteCore.types.NoneAlignment);
                        }
                        checked = Qt.binding(function() { return plasmoid.location === edge })
                    }
                }
                LatteComponents.Button {
                    id: leftEdgeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("left location", "Left")
                    icon.name: "arrow-left"
                    checked: plasmoid.location === edge
                    checkable: true

                    readonly property int edge: PlasmaCore.Types.LeftEdge

                    onClicked: {
                        if (viewConfig.isReady && plasmoid.location !== edge) {
                            latteView.positioner.setNextLocation("", latteView.screensGroup, "", edge, LatteCore.types.NoneAlignment);
                        }
                        checked = Qt.binding(function() { return plasmoid.location === edge })
                    }
                }
                LatteComponents.Button {
                    id: topEdgeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("top location", "Top")
                    icon.name: "arrow-up"
                    checked: plasmoid.location === edge
                    checkable: true

                    readonly property int edge: PlasmaCore.Types.TopEdge

                    onClicked: {
                        if (viewConfig.isReady && plasmoid.location !== edge) {
                            latteView.positioner.setNextLocation("", latteView.screensGroup, "", edge, LatteCore.types.NoneAlignment);
                        }
                        checked = Qt.binding(function() { return plasmoid.location === edge })
                    }
                }
                LatteComponents.Button {
                    id: rightEdgeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("right location", "Right")
                    icon.name: "arrow-right"
                    checked: plasmoid.location === edge
                    checkable: true

                    readonly property int edge: PlasmaCore.Types.RightEdge

                    onClicked: {
                        if (viewConfig.isReady && plasmoid.location !== edge) {
                            latteView.positioner.setNextLocation("", latteView.screensGroup, "", edge, LatteCore.types.NoneAlignment);
                        }
                        checked = Qt.binding(function() { return plasmoid.location === edge })
                    }
                }
            }
        }
        //! END: Location

        //! BEGIN: Alignment
        ColumnLayout {
            Layout.fillWidth: true
            spacing: units.smallSpacing

            LatteComponents.Header {
                text: i18n("Alignment")
            }

            RowLayout {
                id: alignmentRow
                Layout.fillWidth: true
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2
                LayoutMirroring.enabled: false
                spacing: 2

                readonly property int currentAlignment: latteView && latteView.alignment !== LatteCore.types.NoneAlignment ? latteView.alignment : plasmoid.configuration.alignment
                readonly property int buttonSize: (dialog.optionsWidth - (spacing * 3)) / 4
                function applyAlignment(alignment) {
                    if (plasmoid.configuration.alignment !== alignment) {
                        plasmoid.configuration.alignment = alignment;
                    }

                    if (latteView && latteView.alignment !== alignment) {
                        latteView.alignment = alignment;
                    }

                    latteView.positioner.setNextLocation("", latteView.screensGroup, "", PlasmaCore.Types.Floating, alignment);
                }

                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: panelIsVertical ? i18nc("top alignment", "Top") : i18nc("left alignment", "Left")
                    icon.name: panelIsVertical ? "format-align-vertical-top" : "format-justify-left"
                    checked: parent.currentAlignment === alignment
                    checkable: true

                    property int alignment: panelIsVertical ? LatteCore.types.Top : LatteCore.types.Left

                    onClicked: {
                        parent.applyAlignment(alignment)
                        checked = Qt.binding(function() { return parent.currentAlignment === alignment })
                    }
                }
                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("center alignment", "Center")
                    icon.name: panelIsVertical ? "format-align-vertical-center" : "format-justify-center"
                    checked: parent.currentAlignment === alignment
                    checkable: true

                    property int alignment: LatteCore.types.Center

                    onClicked: {
                        parent.applyAlignment(alignment)
                        checked = Qt.binding(function() { return parent.currentAlignment === alignment })
                    }
                }
                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: panelIsVertical ? i18nc("bottom alignment", "Bottom") : i18nc("right alignment", "Right")
                    icon.name: panelIsVertical ? "format-align-vertical-bottom" : "format-justify-right"
                    checked: parent.currentAlignment === alignment
                    checkable: true

                    property int alignment: panelIsVertical ? LatteCore.types.Bottom : LatteCore.types.Right

                    onClicked: {
                        parent.applyAlignment(alignment)
                        checked = Qt.binding(function() { return parent.currentAlignment === alignment })
                    }
                }

                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18nc("justify alignment", "Justify")
                    icon.name: "format-justify-fill"
                    checked: parent.currentAlignment === alignment
                    checkable: true

                    property int alignment: LatteCore.types.Justify

                    onClicked: {
                        parent.applyAlignment(alignment)
                        checked = Qt.binding(function() { return parent.currentAlignment === alignment })
                    }
                }
            }
        }
        //! END: Alignment

        //! BEGIN: Visibility
        ColumnLayout {
            Layout.fillWidth: true
            spacing: units.smallSpacing

            LatteComponents.Header {
                text: i18n("Visibility")
            }

            GridLayout {
                width: parent.width
                rowSpacing: 1
                columnSpacing: 2
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2

                columns: 2

                property int mode: latteView.visibility.mode
                readonly property int buttonSize: (dialog.optionsWidth - (columnSpacing)) / 2

                LatteComponents.Button {
                    id:alwaysVisibleBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18n("Always Visible")
                    checked: parent.mode === mode
                    checkable: true

                    property int mode: LatteCore.types.AlwaysVisible

                    onClicked: {
                        latteView.visibility.mode = mode
                        checked = Qt.binding(function() { return parent.mode === mode })
                    }
                }
                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18n("Auto Hide")
                    checked: parent.mode === mode
                    checkable: true

                    property int mode: LatteCore.types.AutoHide

                    onClicked: {
                        latteView.visibility.mode = mode
                        checked = Qt.binding(function() { return parent.mode === mode })
                    }
                }
                LatteComponents.Button {
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    text: i18n("Dodge Active")
                    checked: parent.mode === mode
                    checkable: true

                    property int mode: LatteCore.types.DodgeActive

                    onClicked: {
                        latteView.visibility.mode = mode
                        checked = Qt.binding(function() { return parent.mode === mode })
                    }
                }

                LatteExtraControls.CustomVisibilityModeButton {
                    id: dodgeModeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    implicitWidth: alwaysVisibleBtn.implicitWidth
                    implicitHeight: alwaysVisibleBtn.implicitHeight

                    checked: containsActiveMode

                    mode: plasmoid.configuration.lastDodgeVisibilityMode
                    modes: [
                        {
                            pluginId: LatteCore.types.DodgeMaximized,
                            name: i18n("Dodge Maximized"),
                            tooltip: ""
                        },
                        {
                            pluginId: LatteCore.types.DodgeAllWindows,
                            name: i18n("Dodge All Windows"),
                            tooltip: ""
                        }
                    ]

                    onViewRelevantVisibilityModeChanged: plasmoid.configuration.lastDodgeVisibilityMode = latteView.visibility.mode;
                }

                LatteExtraControls.CustomVisibilityModeButton {
                    id: windowsModeBtn
                    Layout.minimumWidth: parent.buttonSize
                    Layout.maximumWidth: Layout.minimumWidth
                    implicitWidth: alwaysVisibleBtn.implicitWidth
                    implicitHeight: alwaysVisibleBtn.implicitHeight

                    checked: containsActiveMode

                    mode: plasmoid.configuration.lastWindowsVisibilityMode
                    modes: [
                        {
                            pluginId: LatteCore.types.WindowsGoBelow,
                            name: i18n("Windows Go Below"),
                            tooltip: ""
                        },
                        {
                            pluginId: LatteCore.types.WindowsCanCover,
                            name: i18n("Windows Can Cover"),
                            tooltip: ""
                        },
                        {
                            pluginId: LatteCore.types.WindowsAlwaysCover,
                            name: i18n("Windows Always Cover"),
                            tooltip: ""
                        }
                    ]

                    onViewRelevantVisibilityModeChanged: plasmoid.configuration.lastWindowsVisibilityMode = latteView.visibility.mode;
                }

            }
        }
        //! END: Visibility

        //! BEGIN: Delay
        ColumnLayout {
            Layout.fillWidth: true
            spacing: units.smallSpacing

            LatteComponents.Header {
                text: i18n("Delay")
            }

            Flow {
                width: dialog.optionsWidth
                Layout.minimumWidth: dialog.optionsWidth
                Layout.maximumWidth: dialog.optionsWidth
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2
                Layout.topMargin: units.smallSpacing

                spacing: 2

                readonly property bool overlap: showContainer.overlap || hideContainer.overlap

                Item {
                    id: showContainer
                    width: parent.overlap ? dialog.optionsWidth : oneLineWidth
                    height: childrenRect.height
                    implicitWidth: width
                    implicitHeight: height

                    readonly property bool overlap: oneLineWidth > alwaysVisibleBtn.width
                    readonly property int oneLineWidth: Math.max(alwaysVisibleBtn.width, showTimerRow.width)

                    RowLayout{
                        id: showTimerRow
                        anchors.horizontalCenter: parent.horizontalCenter
                        PlasmaComponents.Label {
                            Layout.leftMargin: Qt.application.layoutDirection === Qt.RightToLeft ? units.smallSpacing : 0
                            Layout.rightMargin: Qt.application.layoutDirection === Qt.RightToLeft ? 0 : units.smallSpacing
                            text: i18n("Show ")
                        }

                        LatteComponents.TextField {
                            Layout.preferredWidth: implicitWidth
                            text: latteView.visibility.timerShow

                            onValueChanged: {
                                latteView.visibility.timerShow = value
                            }
                        }
                    }
                }

                Item {
                    id: hideContainer
                    width: parent.overlap ? dialog.optionsWidth : oneLineWidth
                    height: childrenRect.height
                    implicitWidth: width
                    implicitHeight: height

                    readonly property bool overlap: oneLineWidth > alwaysVisibleBtn.width
                    readonly property int oneLineWidth: Math.max(alwaysVisibleBtn.width, hideTimerRow.width)

                    RowLayout {
                        id: hideTimerRow
                        anchors.horizontalCenter: parent.horizontalCenter

                        PlasmaComponents.Label {
                            Layout.leftMargin: Qt.application.layoutDirection === Qt.RightToLeft ? units.smallSpacing : 0
                            Layout.rightMargin: Qt.application.layoutDirection === Qt.RightToLeft ? 0 : units.smallSpacing
                            text: i18n("Hide")
                        }

                        LatteComponents.TextField{
                            Layout.preferredWidth: implicitWidth
                            text: latteView.visibility.timerHide
                            maxValue: 5000

                            onValueChanged: {
                                latteView.visibility.timerHide = value
                            }
                        }
                    }
                }
            }
        }
        //! END: Delay

        //! BEGIN: Actions
        ColumnLayout {
            spacing: units.smallSpacing
            visible: dialog.advancedLevel

            LatteComponents.Header {
                text: i18n("Actions")
            }

            ColumnLayout {
                id: actionsPropertiesColumn
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2
                spacing: 0

                readonly property int maxLabelWidth: Math.max(trackActiveLbl.implicitWidth,
                                                              mouseWheelLbl.implicitWidth,
                                                              leftBtnLbl.implicitWidth,
                                                              midBtnLbl.implicitWidth)

                ColumnLayout {
                    RowLayout {
                        Layout.topMargin: units.smallSpacing

                        PlasmaComponents.Label {
                            id: trackActiveLbl
                            Layout.minimumWidth: actionsPropertiesColumn.maxLabelWidth
                            Layout.maximumWidth: actionsPropertiesColumn.maxLabelWidth
                            text: i18nc("track active window","Track")
                        }

                        LatteComponents.ComboBox {
                            id: activeWindowFilterCmb
                            Layout.fillWidth: true
                            model: [i18nc("track from current screen", "Active Window From Current Screen"),
                                i18nc("track from all screens", "Active Window From All Screens")]

                            currentIndex: plasmoid.configuration.activeWindowFilter

                            onCurrentIndexChanged: {
                                switch(currentIndex) {
                                case LatteContainment.types.ActiveInCurrentScreen:
                                    plasmoid.configuration.activeWindowFilter = LatteContainment.types.ActiveInCurrentScreen;
                                    break;
                                case LatteContainment.types.ActiveFromAllScreens:
                                    plasmoid.configuration.activeWindowFilter = LatteContainment.types.ActiveFromAllScreens;
                                    break;
                                }
                            }
                        }
                    }
                }

                ColumnLayout {
                    Layout.topMargin: units.smallSpacing
                    RowLayout {
                        PlasmaComponents.Label {
                            id: leftBtnLbl
                            Layout.minimumWidth: actionsPropertiesColumn.maxLabelWidth
                            Layout.maximumWidth: actionsPropertiesColumn.maxLabelWidth
                            text: i18n("Left Button")
                        }

                        LatteComponents.Button {
                            Layout.fillWidth: true
                            text: i18n("Drag Active Window")
                            tooltip: i18n("The user can use left mouse button to drag and maximized/restore last active window from empty areas")
                            checkable: true
                            icon.name: "transform-move"

                            readonly property int dragActiveWindowEnabled: plasmoid.configuration.dragActiveWindowEnabled

                            onDragActiveWindowEnabledChanged: checked = dragActiveWindowEnabled

                            onClicked: {
                                plasmoid.configuration.dragActiveWindowEnabled = checked;
                            }
                        }
                    }

                    RowLayout {
                        PlasmaComponents.Label {
                            id: midBtnLbl
                            Layout.minimumWidth: actionsPropertiesColumn.maxLabelWidth
                            Layout.maximumWidth: actionsPropertiesColumn.maxLabelWidth
                            text: i18n("Middle Button")
                        }

                        LatteComponents.Button {
                            Layout.fillWidth: true
                            text: i18n("Close Active Window")
                            tooltip: i18n("The user can use middle mouse button to close last active window from empty areas")
                            checkable: true
                            icon.name: "window-close"

                            readonly property int closeActiveWindowEnabled: plasmoid.configuration.closeActiveWindowEnabled

                            onCloseActiveWindowEnabledChanged: checked = closeActiveWindowEnabled;

                            onClicked: {
                                plasmoid.configuration.closeActiveWindowEnabled = checked;
                            }
                        }
                    }

                    RowLayout {
                       // Layout.topMargin: units.smallSpacing

                        PlasmaComponents.Label {
                            id: mouseWheelLbl
                            Layout.minimumWidth: actionsPropertiesColumn.maxLabelWidth
                            Layout.maximumWidth: actionsPropertiesColumn.maxLabelWidth
                            text: i18n("Mouse wheel")
                        }

                        LatteComponents.ComboBox {
                            id: scrollAction
                            Layout.fillWidth: true
                            model: [i18nc("none scroll actions", "No Action"),
                                i18n("Cycle Through Desktops"),
                                i18n("Cycle Through Activities"),
                                i18n("Cycle Through Tasks"),
                                i18n("Cycle And Minimize Tasks")]

                            currentIndex: plasmoid.configuration.scrollAction

                            onActivated: {
                                if (plasmoid.configuration.scrollAction !== currentIndex) {
                                    plasmoid.configuration.scrollAction = currentIndex;
                                }
                            }
                        }
                    }
                }

                LatteComponents.SubHeader {
                    text: i18n("Items")
                }

                LatteComponents.CheckBoxesColumn {
                    LatteComponents.CheckBox {
                        id: titleTooltipsChk
                        Layout.maximumWidth: dialog.optionsWidth
                        text: i18n("Thin title tooltips on hovering")
                        tooltip: i18n("Show narrow tooltips produced by Latte for items.\nThese tooltips are not drawn when applets zoom effect is disabled");
                        value: plasmoid.configuration.titleTooltips

                        onClicked: {
                            plasmoid.configuration.titleTooltips = !plasmoid.configuration.titleTooltips;
                        }
                    }

                    LatteComponents.CheckBox {
                        id: mouseWheelChk
                        Layout.maximumWidth: dialog.optionsWidth
                        text: i18n("Expand popup through mouse wheel")
                        tooltip: i18n("Show or Hide applet popup through mouse wheel action")
                        value: plasmoid.configuration.mouseWheelActions
                        visible: dialog.advancedLevel

                        onClicked: {
                            plasmoid.configuration.mouseWheelActions = !plasmoid.configuration.mouseWheelActions;
                        }
                    }

                    LatteComponents.CheckBox {
                        id: autoSizeChk
                        Layout.maximumWidth: dialog.optionsWidth
                        text: i18n("Adjust size automatically when needed")
                        tooltip: i18n("Items decrease their size when exceed maximum length and increase it when they can fit in")
                        value: plasmoid.configuration.autoSizeEnabled
                        visible: dialog.advancedLevel

                        onClicked: {
                            plasmoid.configuration.autoSizeEnabled = !plasmoid.configuration.autoSizeEnabled;
                        }
                    }

                    LatteComponents.CheckBox {
                        Layout.maximumWidth: dialog.optionsWidth
                       // Layout.maximumHeight: mouseWheelChk.height
                        text: i18n("Activate based on position global shortcuts")
                        tooltip: i18n("This view is used for based on position global shortcuts. Take note that only one view can have that option enabled for each layout")
                        value: latteView.isPreferredForShortcuts || (!latteView.layout.preferredForShortcutsTouched && latteView.isHighestPriorityView())

                        onClicked: {
                            latteView.isPreferredForShortcuts = checked;
                            if (!latteView.layout.preferredForShortcutsTouched) {
                                latteView.layout.preferredForShortcutsTouched = true;
                            }
                        }
                    }
                }
            }

            LatteComponents.SubHeader {
                id: floatingSubCategory
                text: i18n("Floating")
            }

            LatteComponents.CheckBoxesColumn {
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2

                LatteComponents.CheckBoxesColumn {
                    LatteComponents.CheckBox {
                        Layout.maximumWidth: dialog.optionsWidth
                        text: i18n("Always use floating gap for user interaction")
                        tooltip: i18n("Floating gap is always used for applets and window interaction")
                        value: plasmoid.configuration.floatingInternalGapIsForced
                        enabled: plasmoid.configuration.zoomLevel === 0

                        onClicked: {
                            plasmoid.configuration.floatingInternalGapIsForced = !plasmoid.configuration.floatingInternalGapIsForced;
                        }
                    }

                    LatteComponents.CheckBox {
                        Layout.maximumWidth: dialog.optionsWidth
                        text: i18n("Hide floating gap for maximized windows")
                        tooltip: i18n("Floating gap is disabled when there are maximized windows")
                        value: plasmoid.configuration.hideFloatingGapForMaximized

                        onClicked: {
                            plasmoid.configuration.hideFloatingGapForMaximized = !plasmoid.configuration.hideFloatingGapForMaximized;
                        }
                    }

                    LatteComponents.CheckBox {
                        Layout.maximumWidth: dialog.optionsWidth
                        enabled: plasmoid.configuration.hideFloatingGapForMaximized
                        text: i18n("Delay floating gap hiding until mouse leaves")
                        tooltip: i18n("to avoid clicking on adjacent items accidentally in some cases")
                        value: plasmoid.configuration.floatingGapHidingWaitsMouse

                        onClicked: {
                            plasmoid.configuration.floatingGapHidingWaitsMouse = !plasmoid.configuration.floatingGapHidingWaitsMouse;
                        }
                    }

                    LatteComponents.CheckBox {
                        Layout.maximumWidth: dialog.optionsWidth
                        enabled: latteView.visibility.mode === LatteCore.types.AlwaysVisible
                        text: i18n("Mirror floating gap when it is shown")
                        tooltip: i18n("Floating gap is mirrored when it is shown in Always Visible mode")
                        value: plasmoid.configuration.floatingGapIsMirrored

                        onClicked: {
                            plasmoid.configuration.floatingGapIsMirrored = !plasmoid.configuration.floatingGapIsMirrored;
                        }
                    }
                }
            }
        }
        //! END: Actions

        //! BEGIN: Adjust
        ColumnLayout {
            spacing: units.smallSpacing

            visible: dialog.advancedLevel

            LatteComponents.Header {
                text: i18n("Environment")
            }

            LatteComponents.CheckBoxesColumn {
                Layout.leftMargin: units.smallSpacing * 2
                Layout.rightMargin: units.smallSpacing * 2

                LatteComponents.CheckBox {
                    Layout.maximumWidth: dialog.optionsWidth
                    text: i18n("Raise on desktop change")
                    value: latteView.visibility.raiseOnDesktop

                    onClicked: {
                        latteView.visibility.raiseOnDesktop = !latteView.visibility.raiseOnDesktop;
                    }
                }
            }
        }
        //! END: Adjust

    }
}
